version = libraryVersions.idscp

apply plugin: 'maven-publish'

dependencies {
    providedByBundle project(':ids-api')

    providedByBundle group: 'com.github.microsoft', name: 'TSS.Java', version: '0.3.0'

    providedByBundle group: 'org.apache.logging.log4j', name: 'log4j-core', version: libraryVersions.log4j
    providedByBundle group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: libraryVersions.log4j

    providedByBundle group: 'com.google.protobuf', name: 'protobuf-java', version: libraryVersions.protobuf
    
    compileOnly group: 'org.checkerframework', name: 'checker-qual', version: libraryVersions.checkerQual

    providedByBundle group: 'org.asynchttpclient', name: 'async-http-client', version: libraryVersions.ahc

    providedByFeature group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: libraryVersions.jetty
    providedByFeature group: 'org.eclipse.jetty.websocket', name: 'websocket-client', version: libraryVersions.jetty

    testImplementation group: "javax.xml.bind", name: "jaxb-api", version: libraryVersions.jaxb
    testImplementation group: "com.sun.xml.bind", name: "jaxb-core", version: libraryVersions.jaxb
    testImplementation group: "com.sun.xml.bind", name: "jaxb-impl", version: libraryVersions.jaxb

    testImplementation group: 'junit', name: 'junit', version: libraryVersions.junit4
    testImplementation group: 'org.mockito', name: 'mockito-all', version: libraryVersions.mockito
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        commLibrary(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Trusted Connector API Library'
                description = 'Contains common data structures and interfaces used by the Trusted Connector and IDSCP'
                url = 'https://github.com/industrial-data-space/trusted-connector'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Michael Lux'
                        email = 'michael.lux@aisec.fraunhofer.de'
                        organization = 'Fraunhofer AISEC'
                        organizationUrl = 'aisec.fraunhofer.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:industrial-data-space/trusted-connector.git'
                    developerConnection = 'scm:git:ssh://github.com:industrial-data-space/trusted-connector.git'
                    url = 'https://github.com/industrial-data-space/trusted-connector'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

//signing {
//    sign publishing.publications.commLibrary
//}